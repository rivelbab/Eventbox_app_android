plugins {
    id "com.diffplug.gradle.spotless" version "3.28.1"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: "com.github.b3er.local.properties"

def STRIPE_API_TOKEN = System.getenv('STRIPE_API_TOKEN') ?: "YOUR_API_KEY"
def MAPBOX_KEY = System.getenv('MAPBOX_KEY') ?: "pk.eyJ1IjoiYW5nbWFzMSIsImEiOiJjanNqZDd0N2YxN2Q5NDNuNTBiaGt6eHZqIn0.BCrxjW6rP_OuOuGtbhVEQg"
def PAYPAL_CLIENT_ID = System.getenv('PAYPAL_CLIENT_ID') ?: "YOUR_API_KEY"

android {
    dataBinding {
        enabled = true
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.eventbox.app.android"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "DEFAULT_BASE_URL", '"https://eventbox-api-dev.herokuapp.com/v1/"'
            buildConfigField "String", "MAPBOX_KEY", '"' + MAPBOX_KEY + '"'
            buildConfigField "String", "STRIPE_API_KEY", '"' + STRIPE_API_TOKEN + '"'
            buildConfigField "String", "PAYPAL_CLIENT_ID", '"' + PAYPAL_CLIENT_ID + '"'
            resValue "string", "FRONTEND_HOST", "eventbox.com"
        }
        debug {
            buildConfigField "String", "DEFAULT_BASE_URL", '"https://eventbox-api-dev.herokuapp.com/v1/"'
            buildConfigField "String", "MAPBOX_KEY", '"' + MAPBOX_KEY + '"'
            buildConfigField "String", "STRIPE_API_KEY", '"' + STRIPE_API_TOKEN + '"'
            buildConfigField "String", "PAYPAL_CLIENT_ID", '"' + PAYPAL_CLIENT_ID + '"'
            resValue "string", "FRONTEND_HOST", "eventbox.com"
        }
    }

    flavorDimensions "default"

    productFlavors {
        playStore {
            dimension "default"
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    // Exclusions added for Paypal SDK
    packagingOptions {
        pickFirst 'kotlin/**'
        exclude 'lib/arm64-v8a/libcardioDecider.so'
        exclude 'lib/arm64-v8a/libcardioRecognizer.so'
        exclude 'lib/arm64-v8a/libcardioRecognizer_tegra2.so'
        exclude 'lib/arm64-v8a/libopencv_core.so'
        exclude 'lib/arm64-v8a/libopencv_imgproc.so'
        exclude 'lib/armeabi/libcardioDecider.so'
        exclude 'lib/armeabi-v7a/libcardioDecider.so'
        exclude 'lib/armeabi-v7a/libcardioRecognizer.so'
        exclude 'lib/armeabi-v7a/libcardioRecognizer_tegra2.so'
        exclude 'lib/armeabi-v7a/libopencv_core.so'
        exclude 'lib/armeabi-v7a/libopencv_imgproc.so'
        exclude 'lib/mips/libcardioDecider.so'
        exclude 'lib/x86/libcardioDecider.so'
        exclude 'lib/x86/libcardioRecognizer.so'
        exclude 'lib/x86/libcardioRecognizer_tegra2.so'
        exclude 'lib/x86/libopencv_core.so'
        exclude 'lib/x86/libopencv_imgproc.so'
        exclude 'lib/x86_64/libcardioDecider.so'
        exclude 'lib/x86_64/libcardioRecognizer.so'
        exclude 'lib/x86_64/libcardioRecognizer_tegra2.so'
        exclude 'lib/x86_64/libopencv_core.so'
        exclude 'lib/x86_64/libopencv_imgproc.so'
    }
    lintOptions {
        disable 'MissingTranslation'
        warning 'InvalidPackage'
    }
    androidExtensions {
        experimental = true
    }
    aaptOptions {
        cruncherEnabled = false
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

}

spotless {
    kotlin {
        ktlint().userData(['android'        : 'true', 'color': 'true',
                           'max_line_length': '120', 'reporter': 'checkstyle'])
        target '**/*.kt'
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    def lifecycle_version = "2.2.0"
    def roomVersion = "2.2.5"
    def koin_version = "2.0.1"
    def ktx_version = '1.2.4'
    def ktx2_version = "2.2.0"
    def nav_version = "2.2.1"
    def anko_version = "0.10.8"
    def paging_version = "2.1.2"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    //==============================================================
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.2.0-alpha05'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "androidx.browser:browser:1.2.0"
    implementation 'androidx.exifinterface:exifinterface:1.2.0'
    implementation "androidx.lifecycle:lifecycle-extensions:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:${lifecycle_version}"
    implementation "androidx.room:room-runtime:${roomVersion}"
    implementation "androidx.room:room-rxjava2:${roomVersion}"
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.11.0.rc1'
    implementation 'com.github.jasminb:jsonapi-converter:0.10'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.5.0'
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-jackson:2.8.1'
    // KTX
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation "androidx.fragment:fragment-ktx:$ktx_version"
    implementation "androidx.collection:collection-ktx:1.1.0"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$ktx2_version"
    // Koin
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.3'
    // Cards Shimmer Animation
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.8.1'
    // Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'
    // Stripe
    implementation 'com.stripe:stripe-android:14.2.1'
    // QR Code
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'
    //Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    // For Kotlin use navigation-fragment-ktx
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    // For Kotlin use navigation-ui-ktx
    //Anko
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-design:$anko_version"
    //Mapbox java sdk
    implementation 'com.mapbox.mapboxsdk:mapbox-sdk-services:5.1.0'
    // SimpleCropView
    implementation 'com.isseiaoki:simplecropview:1.1.8'
    // Paging
    implementation "androidx.paging:paging-runtime:$paging_version"
    implementation "androidx.paging:paging-rxjava2:$paging_version"
    // Searchable Spinner
    implementation 'com.toptoche.searchablespinner:searchablespinnerlibrary:1.3.1'
    //ExpandableTextView
    implementation 'at.blogc:expandabletextview:1.0.5'
    //ExpandableView
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    //PayPal
    implementation 'com.paypal.sdk:paypal-android-sdk:2.16.0'
    // Location and Smart Auth Play Service
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.android.gms:play-services-auth:18.0.0'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "androidx.room:room-compiler:${roomVersion}"

    // == test implementation
    testImplementation 'junit:junit:4.13'
    testImplementation "androidx.room:room-testing:${roomVersion}"
    testImplementation 'com.github.iamareebjamal:stetho-noop:1.2.1'
    testImplementation "org.koin:koin-test:$koin_version"
    testImplementation 'org.threeten:threetenbp:1.4.3'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    // == Android test implementation
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // == Release implementation
    releaseImplementation 'com.github.iamareebjamal:stetho-noop:1.2.1'

    //== Debug implementation
    debugImplementation 'com.facebook.stetho:stetho:1.5.1'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
}
